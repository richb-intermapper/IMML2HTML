<!--
	Wireless - Tranzeo CPE-5A (com.dartware.wrls.tranzeo.cpe.5a)
	Probe for InterMapper (http://www.intermapper.com)

	Copyright (c) 2005, Dartware, LLC. 	This probe is part of Dartware's InterMapper
	Wireless Add-on package and should not be be distributed or used without license.

	Original version 23 Feb 2005 tex.
-->


<header>
	"type"			= 	"tcp-script"
	"package"		= 	"com.dartware"
	"probe_name"	=	"wrls.tranzeo.cpe.5a"
	"human_name"	=	"Wireless - Tranzeo CPE-5A"
	"version"		= 	"1.4"
	"address_type"	= 	"IP"
	"port_number"	=	"80"
	"equiv_class"	= 	"WRLS"
	"display_name"	=	"Wireless/Tranzeo/Tranzeo CPE-5A"
	"url_hint"		=	"http://${ADDRESS}"
</header>

<description>

\GB\Tranzeo CPE-5A\P\

This probe is meant to monitor a \u2=http://www.tranzeo.com\Tranzeo\p0\ TR-CPE.

This probe is part of the InterMapper Wireless Probe Bundle, and requires InterMapper 4.3 or later.
</description>

<parameters>
	"User"				=		""
	"Password*"			=		""
	"Warning Threshold"		=		"-72"
	"Alarm Threshold"		=		"-80"
</parameters>

<script>
CONN #60
FAIL @FAILED

CHCK "${User}" ELSE @NO_USER

WAIT #60 @IDLE (idle timeout in secs)
DISC @DISCONNECT
SEND "GET /info.js HTTP/1.1\r\n"
CHCK "${_REMOTEADDRESS}" else goto #+3
SEND "Host: ${_REMOTEADDRESS}\r\n"
SEND "User-Agent: InterMapper/${_VERSION}\r\n"
CHCK "${User}${Password*}" ELSE #+2
SEND "Authorization: Basic ${_BASE64:${User}:${Password*}}\r\n"
SEND "\r\n"
SEND "\r\n"

MTCH "HTTP/"i else goto @UNEXPECTED_GREETING
MTCH "200" else goto @UNAVAIL_GET_RESPONSE

STOR "^content_length" #2048
#Scan lines for "Content-Length" or ""
NEXT
MTCH "Content-Length[^0-9]*([0-9]+)"ir else goto #+4
STOR "^content_length" "${1}"
EXPT "^$"r
GOTO #+2
MTCH "^$"r else goto #-5

LINE #${^content_length}
DISC @NO_MATCH
EXPT "data=\"(.*)\""ir

EVAL ${^data} := unpack( "${1}", "#~" )


#The sprintf handles the null-terminated string
EVAL ${^device_name} := sprintf( "%s", substr( "${^data}", 0, 19 ) )

# if (a[42] == 0) then
#	ssid = "Not associated"
# else
#	ssid = (a[42]-a[42+32])
EVAL ${^ssid} := sprintf( "%s", substr( "${^data}", 42, 32 ) )
SBNE "" "${^ssid}" #+2
STOR "^ssid" "Not associated"

EVAL ${^MAC} := sprintf( "%a", substr( "${^data}", 20, 6 ) )

# (from /info.js)
# sec = "Off"
# if (a[77] == 1)
#	sec = "WEP ${keylength}"
# else if (a[77] == 2)
#	sec = "WPA"
# else if (a[77] == 3)
#	sec = "WEP,WPA ${keylength}"
#
# WEP = sec
STOR "^sec" "Off"
EVAL ${^sec_type} := unpack( substr( "${^data}", 77, 1 ), "C" )
NBNE #${^sec_type} #1 #+3
STOR "^sec" "WEP "
GOTO #+6
NBNE #${^sec_type} #2 #+3
STOR "^sec" "WPA"
GOTO #+3
NBNE #${^sec_type} #3 #+2
STOR "^sec" "WEP,WPA "

#Reconnect and get data from /1 to fill in Channel and TX Rate
STOR "RET" "@GET_CHANNEL"
DISC @RECONNECT
SKIP ".*"r ELSE @RECONNECT

GOTO @RECONNECT

@GET_CHANNEL:
WAIT #30 @IDLE (idle timeout in secs)
SEND "GET /1 HTTP/1.0\r\n"
CHCK "${_REMOTEADDRESS}" ELSE #+3
SEND "Host: ${_REMOTEADDRESS}\r\n"
SEND "User-Agent: InterMapper/${_VERSION}\r\n"
CHCK "${User}${Password*}" ELSE #+2
SEND "Authorization: Basic ${_BASE64:${User}:${Password*}}\r\n"
SEND "\r\n"
SEND "\r\n"
DISC @DISCONNECT (disconnect handler)

MTCH "HTTP/"i ELSE @UNEXPECTED_GREETING
MTCH "200" ELSE @BAD_GET_RESPONSE

STOR "^content_length" #2048
#Scan lines for "Content-Length" or ""
NEXT
MTCH "Content-Length[^0-9]*([0-9]+)"ir else goto #+4
STOR "^content_length" "${1}"
EXPT "^$"r
GOTO #+2
MTCH "^$"r else goto #-5

LINE #${^content_length}
DISC @NO_MATCH
EXPT "data=\"(.*)\""ir

EVAL ${^data} := unpack( "${1}", "#~" )

#channel = a[33] from /1
# if ( (channel == 152) || (channel == 160) ) then
#	channel += " Turbo"
EVAL ${^channel} := unpack( substr( "${^data}", 33, 1 ), "C" )

#tx_rate = a[37] from /1
# tx_rate = tx_rate / 2
# if ( tx_rate == 0 )
#	tx_rate = "Automatic"
# 
EVAL ${^tx_rate} := unpack( substr( "${^data}", 37, 1 ), "C" )
NBNE #0 #${^tx_rate} #+3
STOR "^tx_rate" "Automatic"
GOTO #+3
EVAL ${^tx_rate} := ${^tx_rate} / 2
SCAT "^tx_rate" " Mbits/sec"

#Reconnect and get data from /2 to fill in Authentication and keylength
STOR "RET" "@GET_AUTHENTICATION"
DISC @RECONNECT
SKIP ".*"r ELSE @RECONNECT

GOTO @RECONNECT

@GET_AUTHENTICATION:
WAIT #30 @IDLE (idle timeout in secs)
SEND "GET /2 HTTP/1.0\r\n"
CHCK "${_REMOTEADDRESS}" ELSE #+3
SEND "Host: ${_REMOTEADDRESS}\r\n"
SEND "User-Agent: InterMapper/${_VERSION}\r\n"
CHCK "${User}${Password*}" ELSE #+2
SEND "Authorization: Basic ${_BASE64:${User}:${Password*}}\r\n"
SEND "\r\n"
SEND "\r\n"
DISC @DISCONNECT (disconnect handler)

MTCH "HTTP/"i ELSE @UNEXPECTED_GREETING
MTCH "200" ELSE @BAD_GET_RESPONSE

STOR "^content_length" #2048
#Scan lines for "Content-Length" or ""
NEXT
MTCH "Content-Length[^0-9]*([0-9]+)"ir else goto #+4
STOR "^content_length" "${1}"
EXPT "^$"r
GOTO #+2
MTCH "^$"r else goto #-5

LINE #${^content_length}
DISC @NO_MATCH
EXPT "data=\"(.*)\""ir

EVAL ${^data} := unpack( "${1}", "#~" )

STOR "^keylength" " (64)"
# ind = a[1]-1 from /2
# if ( ind == 0 )
#	^keylength = " (128)"
EVAL ${^ind} := unpack( substr( "${^data}", 1, 1 ), "C" )
NBNE #1 #${^ind} #+2
STOR "^keylength" " (128)"

SBNE "${^sec}" ".* $"ir #+2
SCAT "^sec" "${^keylength}"

# (from /2)
# if ( sec == "Off" )
#	auth = sec
# else if ( (int) a[0] - 1 == 1 )
#	auth = "Shared Key"
# else
#	auth = "Open"
SBNE "Off" "${^sec}"i #+3
STOR "^authentication" "Off"
GOTO #+6
EVAL ${^ind} := unpack( substr( "${^data}", 0, 1 ), "C" )
NBNE #2 #${^ind} #+3
STOR "^authentication" "Shared Key"
GOTO #+2
STOR "^authentication" "Open"

#Reconnect and get data from /DataMeasure.js for signal strength
STOR "RET" "@GET_SIGNAL"
DISC @RECONNECT
SKIP ".*"r ELSE @RECONNECT

GOTO @RECONNECT

@GET_SIGNAL:
WAIT #30 @IDLE (idle timeout in secs)
SEND "GET /DataMeasure.js HTTP/1.0\r\n"
CHCK "${_REMOTEADDRESS}" ELSE #+3
SEND "Host: ${_REMOTEADDRESS}\r\n"
SEND "User-Agent: InterMapper/${_VERSION}\r\n"
CHCK "${User}${Password*}" ELSE #+2
SEND "Authorization: Basic ${_BASE64:${User}:${Password*}}\r\n"
SEND "\r\n"
SEND "\r\n"
DISC @DISCONNECT (disconnect handler)

MTCH "HTTP/"i ELSE @UNEXPECTED_GREETING
MTCH "200" ELSE @BAD_GET_RESPONSE

STOR "^content_length" #2048
#Scan lines for "Content-Length" or ""
NEXT
MTCH "Content-Length[^0-9]*([0-9]+)"ir else goto #+4
STOR "^content_length" "${1}"
EXPT "^$"r
GOTO #+2
MTCH "^$"r else goto #-5

LINE #${^content_length}
DISC @NO_MATCH
EXPT "data=\"(.*)\""ir

EVAL ${^data} := unpack( "${1}", "#~" )
#signal = -a[3] from /DataMeasure.js
EVAL ${signal} := unpack( substr( "${^data}", 3, 1 ), "c" )
SBNE "${signal}" "^[^-].*"ir #+2
STOR "signal" "-${signal}"

@DONE:
NBGT #${signal} #${Warning Threshold} #+4
NBGT #${signal} #${Alarm Threshold} #+2
DONE ALRM "Poor signal strength: ${signal}"
DONE WARN "Low signal strength: ${signal}"
DONE OKAY

@NO_MATCH:
DONE WARN "Unrecognized response from device."

@RECONNECT:
RCON
GOTO ${RET}:

@IDLE:
DONE DOWN "No data for ${_IDLETIMEOUT} seconds. [Line ${_IDLELINE}]"

@UNEXPECTED_GREETING:
DONE DOWN "Unexpected greeting from port ${_REMOTEPORT}. (${_LINE:50})"

@UNAVAIL_GET_RESPONSE:
MTCH m"50[0-9]" else goto @BAD_GET_RESPONSE
DONE DOWN "${_LINE:50}"

@BAD_GET_RESPONSE:
MTCH "201" ELSE #+2
DONE WARN "201 Created"
MTCH "202" ELSE #+2
DONE WARN "202 Accepted"
MTCH "204" ELSE #+2
DONE WARN "204 No Content"
MTCH "301" ELSE #+2
DONE WARN "301 Moved Permanently"
MTCH "302" ELSE #+2
DONE WARN "302 Moved Temporarily"
MTCH "304" ELSE #+2
DONE WARN "304 Not Modified"
MTCH "400" ELSE #+2
DONE WARN "400 Bad Request"
MTCH "401" ELSE #+2
DONE WARN "401 Unauthorized"
MTCH "403" ELSE #+2
DONE WARN "Invalid login."
MTCH "404" ELSE #+2
DONE WARN "Unrecognized response (404)."
MTCH "500" ELSE #+2
DONE ALRM "500 Internal Server Error"
MTCH "501" ELSE #+2
DONE ALRM "501 Not Implemented"
MTCH "502" ELSE #+2
DONE ALRM "502 Bad Gateway"
MTCH "503" ELSE #+2
DONE ALRM "503 Service Unavailable"
DONE ALRM "Unknown Error Code ${_LINE:4}"

@DISCONNECT:
DONE DOWN "Disconnected from port ${_REMOTEPORT} after ${_SECSCONNECTED} seconds."

@FAILED:
#use default error message in 4.2.1 and later
DONE DOWN

</script>

<script-output>
\B5\Tranzeo CPE-5A Statistics\P0\
    \4\Device Name:\0\ ${^device_name}
           \4\SSID:\0\ ${^ssid}
        \4\Channel:\0\ ${^channel}
        \4\Tx Rate:\3\ ${^tx_rate}
\4\Signal strength:\0\ ${chartable:-100:+0:$signal}\3\ dBm
   \4\CPE MAC Adrs:\0\ ${^MAC}
            \4\WEP:\0\ ${^sec}
 \4\Authentication:\0\ ${^authentication}
</script-output>
